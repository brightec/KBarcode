apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-allopen"
apply plugin: "com.google.gms.google-services"
apply from: "$project.rootDir/config/quality/quality.gradle"
apply from: "$project.rootDir/config/misc/apknaming.gradle"

allOpen {
    annotation "uk.co.brightec.kbarcode.app.util.OpenClass"
}

import java.text.SimpleDateFormat

static def buildTime() {
    def df = new SimpleDateFormat("dd.MM.yyyy HH.mm")
    df.setTimeZone(TimeZone.getTimeZone("UTC+01:00"))
    return df.format(new Date())
}

static def getBuildNumber() {
    return System.getenv("CIRCLE_BUILD_NUM") as Integer ?: 1
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "uk.co.brightec.kbarcode.app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode getBuildNumber()
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        lintConfig file("$project.rootDir/config/quality/lint.xml")
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "$project.rootDir/config/misc/proguard-rules.pro", "$project.rootDir/proguard-rules.pro"
            testProguardFiles getDefaultProguardFile("proguard-android.txt"), "$project.rootDir/config/misc/proguardTest-rules.pro", "$project.rootDir/proguardTest-rules.pro"
            versionNameSuffix " (DEBUG ${buildTime()})"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "$project.rootDir/config/misc/proguard-rules.pro", "$project.rootDir/proguard-rules.pro"
            testProguardFiles getDefaultProguardFile("proguard-android.txt"), "$project.rootDir/config/misc/proguardTest-rules.pro", "$project.rootDir/proguardTest-rules.pro"
            versionNameSuffix " (#${getBuildNumber()})"
        }
    }
}

dependencies {
    // Use this in your app
//    implementation "uk.co.brightec.kbarcode:kbarcode:$version"
    // We use this for developing the library
    implementation project(":kbarcode")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "com.google.android.material:material:1.3.0"
    implementation platform("com.google.firebase:firebase-bom:28.1.0")
    implementation "com.google.firebase:firebase-core"
    // For CameraX example only
    implementation "androidx.camera:camera-camera2:1.0.0"
    implementation "androidx.camera:camera-lifecycle:1.0.0"
    implementation "androidx.camera:camera-view:1.0.0-alpha25"
    implementation "com.google.android.gms:play-services-mlkit-barcode-scanning:16.1.5"

    //region Local Unit Tests
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:3.10.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    testImplementation "androidx.test:runner:1.3.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    //endregion

    //region Instrumented Tests
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "org.mockito:mockito-android:3.10.0"
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    // UI testing with Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    //endregion
}
